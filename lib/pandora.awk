#!/usr/bin/env gawk

BEGIN {
	pd_null = ""	# Control-N

	pd_runmodset()
	pd_ttymsgset()
}

function pd_runmodset(		f, l, a) {
	pd_foreground = 0
	pd_background = 0

	f = "/proc/" PROCINFO["pid"] "/stat"

	if ((getline l < f) <= 0)
	return

	split(l, a, " ")
	pd_foreground = (a[5] == a[8])
	pd_background = !pd_foreground
}

function pd_ttymsgset() {
	if (pd_ttymsgon == "")
	pd_ttymsgon = pd_foreground

	else if (pd_background)
	pd_ttymsgon = 0
}

# Î— function "pd_readfile" Î´Î­Ï‡ÎµÏ„Î±Î¹ Ï‰Ï‚ Ï€Î±ÏÎ¬Î¼ÎµÏ„ÏÎ¿ Ï„Î¿ ÏŒÎ½Î¿Î¼Î± ÎµÎ½ÏŒÏ‚ Î±ÏÏ‡ÎµÎ¯Î¿Ï… ÎºÎ±Î¹
# ÎµÏ€Î¹ÏƒÏ„ÏÎ­Ï†ÎµÎ¹ Ï‰Ï‚ Î­Î½Î± ÎµÎ½Î¹Î±Î¯Î¿ string Ï„Î¿ Ï€ÎµÏÎ¹ÎµÏ‡ÏŒÎ¼ÎµÎ½Î¿ Ï„Î¿Ï… Î±ÏÏ‡ÎµÎ¯Î¿Ï….
#
# Î£Î—ÎœÎ•Î™Î©Î£Î—: Î‘Î½ Ï„Î¿ Î±ÏÏ‡ÎµÎ¯Î¿ ÎµÎ¯Î½Î±Î¹ Î®Î´Î· Î±Î½Î¿Î¹ÎºÏ„ÏŒ ÎºÎ±Î¹ Î­Ï‡Î¿Ï…Î¼Îµ Î´Î¹Î±Î²Î¬ÏƒÎµÎ¹ Î¼Î­ÏÎ¿Ï‚ Ï„Î¿Ï
# Ï€ÎµÏÎ¹ÎµÏ‡Î¿Î¼Î­Î½Î¿Ï…, Ï„ÏŒÏ„Îµ Î· "pd_readfile" Î¸Î± ÎµÏ€Î¹ÏƒÏ„ÏÎ­ÏˆÎµÎ¹ Ï„Î¿ Ï…Ï€ÏŒÎ»Î¿Î¹Ï€Î¿ Ï€ÎµÏÎ¹ÎµÏ‡ÏŒÎ¼ÎµÎ½Î¿.
# Î£Îµ ÎºÎ¬Î¸Îµ Ï€ÎµÏÎ¯Ï€Ï„Ï‰ÏƒÎ·, Î· "pd_readfile" ÎºÎ»ÎµÎ¯Î½ÎµÎ¹ Ï„Î¿ Î±ÏÏ‡ÎµÎ¯Î¿ Î¼ÎµÏ„Î¬ Ï„Î¿ Ï€Î­ÏÎ±Ï‚ Ï„Î®Ï‚
# Î±Î½Î¬Î³Î½Ï‰ÏƒÎ·Ï‚ Ï„Î¿Ï… Ï€ÎµÏÎ¹ÎµÏ‡Î¿Î¼Î­Î½Î¿Ï….
#
# Î Î¡ÎŸÎ£ÎŸÎ§Î—: Î•Î½Î½Î¿ÎµÎ¯Ï„Î±Î¹ ÏŒÏ„Î¹ Î· "pd_readfile" Î´ÎµÎ½ ÎµÎ¯Î½Î±Î¹ ÎºÎ±Î»ÏŒ Î½Î± Ï‡ÏÎ·ÏƒÎ¹Î¼Î¿Ï€Î¿Î¹ÎµÎ¯Ï„Î±Î¹
# Î³Î¹Î± Ï„Î·Î½ Î±Î½Î¬Î³Î½Ï‰ÏƒÎ· Ï€Î¿Î»Ï Î¼ÎµÎ³Î¬Î»Ï‰Î½ Î±ÏÏ‡ÎµÎ¯Ï‰Î½.

function pd_readfile(file,		s, t) {
	while ((getline s <file) > 0)
	t = t s "\n"

	close(file)
	return t
}

# Î— function "pd_isnull" Î´Î­Ï‡ÎµÏ„Î±Î¹ Î¼Î¹Î± Ï€Î±ÏÎ¬Î¼ÎµÏ„ÏÎ¿ ÎºÎ±Î¹ ÎµÎ»Î­Î³Ï‡ÎµÎ¹ Î±Î½ Î· Ï€Î±ÏÎ¬Î¼ÎµÏ„ÏÎ¿Ï‚ Î±Ï…Ï„Î®
# ÎµÎ¯Î½Î±Î¹ null Î¿Ï€ÏŒÏ„Îµ ÎµÏ€Î¹ÏƒÏ„ÏÎ­Ï†ÎµÎ¹ Î¼Î·Î´Î­Î½, ÎµÎ½Ï Î±Î½ Î· Ï€Î±ÏÎ¬Î¼ÎµÏ„ÏÎ¿Ï‚ Î´ÎµÎ½ ÎµÎ¯Î½Î±Î¹ null Ï„ÏŒÏ„Îµ
# ÎµÏ€Î¹ÏƒÏ„ÏÎ­Ï†ÎµÎ¹ Î¼Î· Î¼Î·Î´ÎµÎ½Î¹ÎºÎ® Ï„Î¹Î¼Î®.

function pd_isnull(val) {
	return (val == pd_null)
}

function pd_notnull(val) {
	return (val != pd_null)
}

# Î— function "pd_nullconvert" ÎµÏ€Î¹ÏƒÏ„ÏÎ­Ï†ÎµÎ¹ Ï„Î·Î½ Ï„Î¹Î¼Î® Ï„Î®Ï‚ Ï€ÏÏÏ„Î·Ï‚ Ï€Î±ÏÎ±Î¼Î­Ï„ÏÎ¿Ï… Ï€Î¿Ï… Ï„Î®Ï‚
# Ï€ÎµÏÎ½Î¬Î¼Îµ, ÎµÎºÏ„ÏŒÏ‚ ÎºÎ±Î¹ Î±Î½ Î±Ï…Ï„Î® ÎµÎ¯Î½Î±Î¹ null Î¿Ï€ÏŒÏ„Îµ ÎµÏ€Î¹ÏƒÏ„ÏÎ­Ï†ÎµÎ¹ Ï„Î·Î½ Ï„Î¹Î¼Î® Ï„Î®Ï‚ Î´ÎµÏÏ„ÎµÏÎ·Ï‚
# Ï€Î±ÏÎ±Î¼Î­Ï„ÏÎ¿Ï….

function pd_nullconvert(s, t) {
	return (s == pd_null ? t : s)
}

###############################################################################@

# Î— function "pd_dt2dt" Ï‡ÏÎ·ÏƒÎ¹Î¼Î¿Ï€Î¿Î¹ÎµÎ¯Ï„Î±Î¹ Î³Î¹Î± Î½Î± Î¼ÎµÏ„Î±Ï„ÏÎ­ÏˆÎµÎ¹ Î·Î¼ÎµÏÎ¿Î¼Î·Î½Î¯ÎµÏ‚ ÎºÎ±Î¹ ÏÏÎµÏ‚
# Î±Ï€ÏŒ Î­Î½Î± format ÏƒÎµ Î¬Î»Î»Î¿. Î¤Î¿ format ÎµÎ¹ÏƒÏŒÎ´Î¿Ï… Î¼Ï€Î¿ÏÎµÎ¯ Î½Î± Ï€ÎµÏÎ¹Î»Î±Î¼Î²Î¬Î½ÎµÎ¹ ÎºÎ¬Ï€Î¿Î¹Î± Î±Ï€ÏŒ
# Ï„Î± Î³ÏÎ¬Î¼Î¼Î±Ï„Î± Y, M, D, h, m ÎºÎ±Î¹ s, ÎµÎ½Ï Ï„Î¿ format ÎµÎ¾ÏŒÎ´Î¿Ï… Î¼Ï€Î¿ÏÎµÎ¯ Î½Î± Ï€ÎµÏÎ¹Î­Ï‡ÎµÎ¹ ÎºÎ±Î¹
# Î¬Î»Î»Î¿Ï…Ï‚ Ï‡Î±ÏÎ±ÎºÏ„Î®ÏÎµÏ‚ Ï€Î¿Ï… Î¸Î± Ï€ÎµÏÎ¹Î»Î·Ï†Î¸Î¿ÏÎ½ Ï‰Ï‚ Î­Ï‡Î¿Ï…Î½.

function pd_dt2dt(date, from, to,		l1, i, l2, \
	n, f, m, len, c, d, v) {

	# ÎšÎ±Î¸Î¿ÏÎ¯Î¶Î¿Î½Ï„Î±Î¹ Ï„Î± Î¼Î®ÎºÎ· Ï„Ï‰Î½ Î´Î¹Î±Ï†ÏŒÏÏ‰Î½ ÏƒÏ…ÏƒÏ„Î±Ï„Î¹ÎºÏÎ½
	# ÏƒÏ„Î¿Î¹Ï‡ÎµÎ¯Ï‰Î½ Î·Î¼ÎµÏÎ¿Î¼Î·Î½Î¯Î±Ï‚ ÎºÎ±Î¹ ÏÏÎ±Ï‚.

	l1["Y"] = 4
	l1["M"] = 2
	l1["D"] = 2
	l1["h"] = 2
	l1["m"] = 2
	l1["s"] = 2

	# Î˜Î± Ï‡ÏÎµÎ¹Î±ÏƒÏ„Î¿ÏÎ¼Îµ Î±Î½Ï„Î¯Î³ÏÎ±Ï†Î¿ Ï„Î·Ï‚ Î»Î¯ÏƒÏ„Î±Ï‚ Î¼Î·ÎºÏÎ½.

	for (i in l1)
	l2[i] = l1[i]

	if (!from)
	from = "YMDhms"

	n = split(from, f, "")

	for (i = 1; i <= n; i++) {
		if (!(f[i] in l1))
		return ""

		delete l2[f[i]]
	}

	for (i in l1)
	l2[i] = l1[i]

	# Î‘Î½ Î· Î·Î¼ÎµÏÎ¿Î¼Î·Î½Î¯Î±/ÏÏÎ± ÎµÎ¹ÏƒÏŒÎ´Î¿Ï… Î´ÎµÎ½ Î±Ï€Î¿Ï„ÎµÎ»ÎµÎ¯Ï„Î±Î¹ Î±Ï€Î¿ÎºÎ»ÎµÎ¹ÏƒÏ„Î¹ÎºÎ¬ Î±Ï€ÏŒ
	# Î±ÏÎ¹Î¸Î¼Î·Ï„Î¹ÎºÎ¬ ÏˆÎ·Ï†Î¯Î±, Ï„ÏŒÏ„Îµ Î¸Î± Ï€ÏÎ­Ï€ÎµÎ¹ Ï„Î± ÏƒÏ…ÏƒÏ„Î±Ï„Î¹ÎºÎ¬ Ï€Î¿Ï… Î´Î·Î»ÏÎ¸Î·ÎºÎ±Î½
	# ÏƒÏ„Î¿ format ÎµÎ¹ÏƒÏŒÎ´Î¿Ï… Î½Î± Ï‡Ï‰ÏÎ¯Î¶Î¿Î½Ï„Î±Î¹ Î¼ÎµÏ„Î±Î¾Ï Ï„Î¿Ï…Ï‚ Î¼Îµ strings Ï€Î¿Ï…
	# Î´ÎµÎ½ Ï€ÎµÏÎ¹Î­Ï‡Î¿Ï…Î½ Î±ÏÎ¹Î¸Î¼Î·Ï„Î¹ÎºÎ¬ ÏˆÎ·Ï†Î¯Î±.

	if (date !~ /^[0-9]+$/) {
		if ((m = split(date, v, /[^0-9]/)) > n)
		return ""

		if (m < 1)
		return ""

		for (i = 1; i <= m; i++) {
			if (v[i] !~ /^[0-9]+$/)
			return ""

			d[f[i]] = v[i]
		}

		if (!to)
		to = "YMDhms"

		return pd_dt2dt_to(d, to, l2)
	}

	# Î‘Î½ Î· Î·Î¼ÎµÏÎ¿Î¼Î·Î½Î¯Î±/ÏÏÎ± ÎµÎ¹ÏƒÏŒÎ´Î¿Ï… Î±Ï€Î¿Ï„ÎµÎ»ÎµÎ¯Ï„Î±Î¹ Î¼ÏŒÎ½Î¿ Î±Ï€ÏŒ Î±ÏÎ¹Î¸Î¼Î·Ï„Î¹ÎºÎ¬ ÏˆÎ·Ï†Î¯Î±,
	# Ï„ÏŒÏ„Îµ Î±Ï€Î¿ÏƒÏ€Î¿ÏÎ¼Îµ Ï„Î± ÏƒÏ…ÏƒÏ„Î±Ï„Î¹ÎºÎ¬ Ï„Î·Ï‚ Î¼Îµ Î²Î¬ÏƒÎ· Ï„Î¿ input format ÎºÎ±Î¹ Ï„Î± Î¼Î®ÎºÎ·
	# Ï„Ï‰Î½ Î±Î½Ï„Î¯ÏƒÏ„Î¿Î¹Ï‡Ï‰Î½ ÏƒÏ…ÏƒÏ„Î±Ï„Î¹ÎºÏÎ½.

	len = length(date)

	for (i = 1; i <= n; i++) {
		c = f[i]

		if (l1[c] > len)
		return ""

		d[c] = substr(date, 1, l1[c]) + 0
		date = substr(date, l1[c] + 1)

		len -= l1[c]

		if (!len)
		break

		delete l1[c]
	}

	if (len)
	return ""

	if (!to)
	to = "Y-M-D h:m:s"

	return pd_dt2dt_to(d, to, l2)
}

function pd_dt2dt_to(date, to, l,		len, i, c, s, t) {
	len = length(to)

	for (i = 1; i <= len; i++) {
		c = substr(to, i, 1)

		if (!(c in l)) {
			t = t c
			continue
		}

		if (!(c in date))
		return ""

		s = s sprintf("%s%0" l[c] "d", t, date[c])

		t = ""
		delete l[c]
	}

	return s
}

function pd_integerck(x, min, max) {
	if (x !~ /^[0-9]+$/)
	return 1

	if ((min != "") && (x < min))
	return 2

	if ((max != "") && (x > max))
	return 3

	return 0
}

function pd_isinteger(x) {
	return (x ~ /^[0-9]+$/)
}

function pd_notinteger(x) {
	return !pd_isinteger(x)
}

###############################################################################@

function pd_aclone(target, source,		i) {
	delete target

	target[""]
	delete target[""]

	if (!isarray(source))
	pd_fatal("pd_aclone: source is not an array")

	for (i in source) {
		if (isarray(source[i]))
		pd_aclone(target[i], source[i])

		else
		target[i] = source[i]
	}
}

function pd_aprint(a, s,		i) {
	if (!isarray(a))
	return

	for (i in a) {
		if (!isarray(a[i])) {
			print s ":" i ": >>" a[i] "<<"
			continue
		}

		print s "[" i "]"
		pd_aprint(a[i], s "\t")
	}
}

###############################################################################@

function pd_errmsg(s, nonl) {
	if (pd_progname)
	printf "%s: ", pd_progname >"/dev/stderr"

	printf "%s", s >"/dev/stderr"

	if (!nonl)
	print "" >"/dev/stderr"

	return 1
}

function pd_fatal(msg, stat) {
	if (msg)
	pd_errmsg(msg)

	exit(stat ? stat : 1)
}

function pd_ttymsg(s, nonl) {
	if (!pd_ttymsgon)
	return

	if (!pd_foreground)
	return

	printf "%s", s >"/dev/tty"

	if (!nonl)
	print "" >"/dev/tty"
}

###############################################################################@

BEGIN {
	srand()
}

function pd_nrand(from, to) {
	return from + int(rand() * (to - from + 1))
}

function pd_srand(len, paleta,			plen, i, s) {
	if (!paleta)
	paleta = "ABCDEFGHIJKLMNOPQRSTUVWXYZ" \
		"abcdefghijklmnopqrstuvwxyz" \
		"0123456789"

	plen = length(paleta)

	while (len-- > 0)
	s = s substr(paleta, pd_nrand(0, plen), 1)

	return s
}

function pd_passgen(l1, l2, paleta) {
	if (!l1)
	l1 = 8

	if (!l2)
	l2 = l1 + pd_nrand(4, 8)

	if (!paleta)
	paleta = "ABCDEFGHIJKLMNOPQRSTUVWXYZ" \
		"abcdefghijklmnopqrstuvwxyz" \
		"0123456789"

	return pd_srand(pd_nrand(l1, l2), paleta)
}

function pd_sha1gen(s,			cmd, x, a) {
	if (!s)
	s = pd_passgen()

	cmd = sprintf("echo -n '%s' | sha1sum", s)
	cmd | getline x
	close(cmd)
	split(x, a, " ")

	return a[1]
}

# Î— function "pd_isempty" ÎµÎ»Î­Î³Ï‡ÎµÎ¹ Î±Î½ Ï„Î¿ array Ï€Î¿Ï… Ï€ÎµÏÎ½Î¬Î¼Îµ Ï‰Ï‚ Ï€Î±ÏÎ¬Î¼ÎµÏ„ÏÎ¿ ÎµÎ¯Î½Î±Î¹
# ÎºÎµÎ½ÏŒ ÎºÎ±Î¹ ÎµÏ€Î¹ÏƒÏ„ÏÎ­Ï†ÎµÎ¹ 1, Î±Î»Î»Î¹ÏÏ‚ ÎµÏ€Î¹ÏƒÏ„ÏÎ­Ï†ÎµÎ¹ 0.

function pd_isempty(list,			i) {
	for (i in list)
	return 0

	return 1
}

function pd_notempty(list) {
	return !pd_isempty(list)
}
